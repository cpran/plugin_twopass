# Generate Pitch object using utterance-specific thresholds
# Using Hirst and De Looze's s two-pass method
#
# This script is part of the twopass plugin for CPrAN.
# The latest versi  on is available through CPrAN or at
# <http://cpran.net/plugins/twopass>
#
# This script is part of the twopass CPrAN plugin for Praat.
# The latest version is available through CPrAN or at
# <http://cpran.net/plugins/twopass>
#
# The twopass plugin is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public
# License as published by the Free Software Foundation, either
# version 3 of the License, or (at your option) any later version.
#
# The twopass plugin is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty
# of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with twopass. If not, see <http://www.gnu.org/licenses/>.
#
# Copyright 2014, 2015 Jose Joaquin Atria

if !variableExists("pitchTwoPass.step")
  pitchTwoPass.step = 0.01
endif

if !variableExists("pitchTwoPass.floor")
  pitchTwoPass.floor = 60
endif

if !variableExists("pitchTwoPass.ceiling")
  pitchTwoPass.ceiling = 700
endif

procedure pitchTwoPass (.floor_factor, .ceiling_factor)
  # Recommended values:
  #   0.75 for floor factor
  #   1.5 for ceiling factor (or 2.5 for expressive speech)

  .floor   = if .floor   == undefined then  60    else .floor   fi
  .ceiling = if .ceiling == undefined then 700    else .ceiling fi
  .step    = if .step    == undefined then   0.01 else .step    fi

  .sound = selected("Sound")

  .first_pass = To Pitch: .step, .floor, .ceiling
  .q1 = Get quantile: 0, 0, 0.25, "Hertz"
  .q3 = Get quantile: 0, 0, 0.75, "Hertz"
  removeObject: .first_pass

  .floor   = if .q1 != undefined then .q1 * .floor_factor   else .floor   fi
  .ceiling = if .q3 != undefined then .q3 * .ceiling_factor else .ceiling fi

  .error = if .floor >= .ceiling then 1 else 0 fi

  selectObject: .sound
  .id = To Pitch: .step, .floor, .ceiling

  # References:
  # Hirst, 2011. "The analysis by synthesis [...]", JoSS 1(1): 55-83
  # De Looze and Hirst, 2008. "Detecting changes in key [...]" @ Speech Prosody
  # Based on scripts available at
  # http://uk.groups.yahoo.com/neo/groups/praat-users/conversations/topics/6199
  # http://celinedelooze.com/MyHomePage/Praat.html
endproc
